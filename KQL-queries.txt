//Some example of KQL queries, not tested in production.

//I want to catch a LOLBins - desktopimgdownldr.exe described here https://labs.sentinelone.com/living-off-windows-land-a-new-native-file-downldr/
//I am using the Sysmon with configuration https://github.com/SwiftOnSecurity/sysmon-config
Event
| where Source == "Microsoft-Windows-Sysmon"
| where RenderedDescription has "desktopimgdownldr.exe"
| extend a = parse_xml(EventData)
| extend CommandLine = tostring(parse_json(tostring(parse_json(tostring(parse_json(tostring(a.DataItem)).EventData)).Data))[10].["#text"])
| project TimeGenerated, CommandLine, Computer, EventData, RenderedDescription, UserName
| sort by TimeGenerated desc 

//I want to catch a LOLBins - CertReq.exe described here https://dtm.uk/certreq/
//This time I used an approach similar to the one used by @DebugPrivilege in https://identityandsecuritydotcom.files.wordpress.com/2020/04/kql_internals_2020.pdf, i.e. parsing EventData using only the parsing operator.
//I leave EventData so that you can always see the contents of all json
Event
| where Source == "Microsoft-Windows-Sysmon" and RenderedDescription has "OriginalFileName: CertReq.exe"
| extend EventFullData = parse_xml(EventData)
| parse EventData with * 'OriginalFileName">'OriginalFileName '</Data>'*
| parse EventData with * 'CommandLine">'Commandline '</Data>'*
| project TimeGenerated, OriginalFileName, Commandline, Computer, EventID, UserName, EventFullData
| sort by TimeGenerated desc 